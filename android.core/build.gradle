apply plugin: "com.android.library"

android {
    compileSdkVersion 20
    buildToolsVersion "20.0.0"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 20
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile project(":core")
}

publishing {
    publications {
        android(MavenPublication) {
            if (!project.tasks.findByPath("bundleRelease")) {
                println "Could not find task 'bundleRelease' for android.core publication. Ignoring publication."
                return
            }

            artifact bundleRelease

            artifact bundleDebug {
                classifier "debug"
            }

            artifact sourceJar

            //TODO automatically grab the right dependencies
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.default.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}
