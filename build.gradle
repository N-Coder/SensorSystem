buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}

allprojects {
    apply plugin: "maven-publish"

    group = "de.ncoder.sensorsystem"
    version = "0.1.4-SNAPSHOT"

    repositories {
        jcenter()
        maven {
            url "https://dl.bintray.com/n-coder/maven/"
        }
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        if (project.hasProperty('android'))
            from project.android.sourceSets.main.java.srcDirs
        else if (project.hasProperty('sourceSets'))
            from project.sourceSets.main.java.srcDirs
    }

    publishing {
        repositories {
            maven {
                name 'Bintray'
                url "https://api.bintray.com/maven/n-coder/maven/SensorSystem"
                if (project.hasProperty("bintrayUsername")) {
                    credentials {
                        username = bintrayUsername
                        password = bintrayApiKey
                    }
                }
            }
        }
    }
}
